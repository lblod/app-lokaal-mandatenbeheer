PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX bestuursperiode: <http://data.lblod.info/id/concept/Bestuursperiode/>
PREFIX lmb: <http://lblod.data.gift/vocabularies/lmb/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX extlmb:  <http://mu.semte.ch/vocabularies/ext/lmb/>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX dct: <http://purl.org/dc/terms/>

DELETE {
  GRAPH ?g {
    ?previousBOIT mandaat:bindingEinde ?bindingEinde .
    ?previousBOIT dct:modified ?modified .
  }
}
INSERT {
  GRAPH ?g {
    ?previousBOIT mandaat:bindingEinde ?earliestStart .
    ?previousBOIT dct:modified ?now .
  }
}
WHERE {
  {
  SELECT DISTINCT ?g ?previousBOIT ?bindingEinde ?modified (MIN(?start) AS ?earliestStart) WHERE {
    GRAPH ?g {
      ?bestuursorgaan a besluit:Bestuursorgaan ;
        besluit:classificatie ?classificatie .
      ?bestuursorgaanIT a besluit:Bestuursorgaan ;
        mandaat:isTijdspecialisatieVan ?bestuursorgaan ;
        lmb:heeftBestuursperiode bestuursperiode:96efb929-5d83-48fa-bfbb-b98dfb1180c7 ; # 2025 - heden
        org:hasPost / ^org:holds ?mandataris .
      ?previousBOIT a besluit:Bestuursorgaan ;
        mandaat:isTijdspecialisatieVan ?bestuursorgaan ;
        lmb:heeftBestuursperiode bestuursperiode:a2b977a3-ce68-4e42-80a6-4397f66fc5ca . # 2019 - heden
      OPTIONAL {
        ?previousBOIT mandaat:bindingEinde ?bindingEinde .
      }
      OPTIONAL {
        ?previousBOIT dct:modified ?modified .
      }
      ?mandataris mandaat:start ?start .
    }
    ?classificatie skos:inScheme extlmb:gemeente-bestuursorgaan-codes .
    ?g ext:ownedBy ?bestuurseenheid .
    ?bestuurseenheid ext:voorbereidingVerborgen true .
  }
  GROUP BY ?g ?previousBOIT ?bindingEinde ?modified
  }
  BIND(NOW() as ?now)
}
