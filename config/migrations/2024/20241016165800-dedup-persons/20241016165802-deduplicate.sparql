PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
DELETE {
  GRAPH ?g {
    ?s ?p ?o.
  }
}
INSERT {
  GRAPH ?g {
    ?newS ?p ?o.
  }
}
WHERE {
  GRAPH ?g {
    ?s ?p ?o.
    FILTER (?p NOT IN (
      <http://purl.org/dc/terms/modified>,
      <http://data.vlaanderen.be/ns/persoon#gebruikteVoornaam>,
      <http://xmlns.com/foaf/0.1/familyName>,
      <http://xmlns.com/foaf/0.1/name>,
      <http://mu.semte.ch/vocabularies/core/uuid>,
      <http://data.vlaanderen.be/ns/persoon#geslacht>,
      <http://data.vlaanderen.be/ns/persoon#heeftGeboorte>,
      <http://www.w3.org/ns/adms#identifier>
    ))
  }
  GRAPH <http://mu.semte.ch/graphs/temp/sameasPersons> {
    ?s owl:sameAs ?newS.
  }
  FILTER (?g != <http://mu.semte.ch/graphs/temp/sameasPersons>)
};
DELETE {
  GRAPH ?g {
    ?o ?p ?s.
  }
}
INSERT {
  GRAPH ?g {
    ?o ?p ?newS.
  }
}
WHERE {
  GRAPH ?g {
    ?o ?p ?s.
  }
  GRAPH <http://mu.semte.ch/graphs/temp/sameasPersons> {
    ?s owl:sameAs ?newS.
  }
  FILTER (?g != <http://mu.semte.ch/graphs/temp/sameasPersons>)
};
DELETE {
  GRAPH ?g {
    ?s ?p ?o.
  }
}
INSERT {
  GRAPH ?g {
    ?newS ?p ?o.
  }
}
WHERE {
  GRAPH ?g {
    ?s ?p ?o.
    FILTER (?p NOT IN (skos:notation))
  }
  GRAPH <http://mu.semte.ch/graphs/temp/sameasIdentifiers> {
    ?s owl:sameAs ?newS.
  }
  FILTER (?g != <http://mu.semte.ch/graphs/temp/sameasIdentifiers>)
};
DELETE {
  GRAPH ?g {
    ?o ?p ?s.
  }
}
INSERT {
  GRAPH ?g {
    ?o ?p ?newS.
  }
}
WHERE {
  GRAPH ?g {
    ?o ?p ?s.
  }
  GRAPH <http://mu.semte.ch/graphs/temp/sameasIdentifiers> {
    ?s owl:sameAs ?newS.
  }
  FILTER (?g != <http://mu.semte.ch/graphs/temp/sameasIdentifiers>)
}