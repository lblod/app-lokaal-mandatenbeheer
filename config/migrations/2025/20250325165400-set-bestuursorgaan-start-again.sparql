PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX bestuursperiode: <http://data.lblod.info/id/concept/Bestuursperiode/>
PREFIX lmb: <http://lblod.data.gift/vocabularies/lmb/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX extlmb:  <http://mu.semte.ch/vocabularies/ext/lmb/>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX dct: <http://purl.org/dc/terms/>

DELETE {
  GRAPH ?g {
    ?bestuursorgaanIT mandaat:bindingStart ?bindingStart .
  }
}
INSERT {
  GRAPH ?g {
    ?bestuursorgaanIT mandaat:bindingStart ?earliestStart .
  }
}
WHERE {
  GRAPH ?og {
  ?bestuursorgaanIT a besluit:Bestuursorgaan.
  ?bestuursorgaanIT mandaat:bindingStart ?bindingStart.
    }
  ?og ext:ownedBy / skos:prefLabel ?label

  { SELECT DISTINCT ?g ?bestuursorgaanIT ?bindingStart (MIN(?start) AS ?earliestStart) WHERE {
    GRAPH ?g {
      ?bestuursorgaan a besluit:Bestuursorgaan ;
      besluit:classificatie ?classificatie .
      ?bestuursorgaanIT a besluit:Bestuursorgaan ;
      mandaat:isTijdspecialisatieVan ?bestuursorgaan ;
      lmb:heeftBestuursperiode bestuursperiode:96efb929-5d83-48fa-bfbb-b98dfb1180c7 ; # 2024 - heden
      org:hasPost / ^org:holds ?mandataris .
      OPTIONAL {
        ?bestuursorgaanIT mandaat:bindingStart ?bindingStart .
      }
      ?mandataris mandaat:start ?start .
    }
    ?classificatie skos:inScheme extlmb:gemeente-bestuursorgaan-codes .
    ?g ext:ownedBy ?bestuurseenheid .
    ?bestuurseenheid ext:voorbereidingVerborgen true .
  } GROUP BY ?g ?bestuursorgaanIT ?bindingStart
  }
  FILTER(?bindingStart != ?earliestStart)
}
