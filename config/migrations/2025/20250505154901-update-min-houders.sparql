PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX lmb: <http://lblod.data.gift/vocabularies/lmb/>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

DELETE {
  GRAPH ?g {
    ?vbMandaat mandaat:aantalHouders ?houders .
  }
}
INSERT {
  GRAPH ?g {
    ?vbMandaat mandaat:aantalHouders ?newHouders .
  }
}
WHERE {
  ?orgaan besluit:classificatie <http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/5ab0e9b8a3b2ca7c5e000008> . # Vast Bureau
  ?orgaan besluit:bestuurt ?ocmwVoorEenheid .
  ?ocmwVoorEenheid ext:isOCMWVoor ?eenheid .
  ?boi mandaat:isTijdspecialisatieVan ?orgaan .
  ?boi lmb:heeftBestuursperiode <http://data.lblod.info/id/concept/Bestuursperiode/96efb929-5d83-48fa-bfbb-b98dfb1180c7> .
  FILTER NOT EXISTS {
    ?eenheid lmb:faciliteitenGemeente true .
  }
  ?vbMandaat org:role <http://data.vlaanderen.be/id/concept/BestuursfunctieCode/5ab0e9b8a3b2ca7c5e000017> . # Lid Vast Bureau
  ?boi org:hasPost ?vbMandaat .
  GRAPH ?g {
    ?vbMandaat mandaat:aantalHouders ?houders .
  }
  ?g ext:ownedBy ?someone .
  BIND(xsd:integer(?houders) + 1 AS ?newHouders)
}
