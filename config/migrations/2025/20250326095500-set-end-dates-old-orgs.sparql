PREFIX bestuursperiode: <http://data.lblod.info/id/concept/Bestuursperiode/>
PREFIX lmb: <http://lblod.data.gift/vocabularies/lmb/>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX dct: <http://purl.org/dc/terms/>
DELETE {
  GRAPH ?g {
    ?orgTold mandaat:bindingEinde ?einde.
  }
}
INSERT {
  GRAPH ?g {
    ?orgTold mandaat:bindingEinde ?start.
  }
}
WHERE {
  ?bestuurseenheid skos:prefLabel ?label.
  ?orgT mandaat:isTijdspecialisatieVan / besluit:bestuurt ?bestuurseenheid.
  ?orgT lmb:heeftBestuursperiode bestuursperiode:96efb929-5d83-48fa-bfbb-b98dfb1180c7.
  ?orgT mandaat:isTijdspecialisatieVan / besluit:classificatie ?clas.
  ?orgT mandaat:bindingStart ?start.
  ?orgTold mandaat:isTijdspecialisatieVan / besluit:bestuurt ?bestuurseenheid.
  ?orgTold lmb:heeftBestuursperiode bestuursperiode:a2b977a3-ce68-4e42-80a6-4397f66fc5ca.
  GRAPH ?g {
    ?orgTold mandaat:isTijdspecialisatieVan / besluit:classificatie ?clas.
    OPTIONAL {
      ?orgTold mandaat:bindingEinde ?einde.
    }
  }
  FILTER (?einde != ?start)
};
DELETE {
  GRAPH ?g {
    ?mandataris dct:modified ?mod.
    ?mandataris mandaat:einde ?mandaatEinde.
  }
}
INSERT {
  GRAPH ?g {
    ?mandataris dct:modified ?now.
    ?mandataris mandaat:einde ?einde.
  }
}
WHERE {
  ?bestuurseenheid skos:prefLabel ?label.
  ?orgT mandaat:isTijdspecialisatieVan / besluit:bestuurt ?bestuurseenheid.
  ?orgT lmb:heeftBestuursperiode bestuursperiode:a2b977a3-ce68-4e42-80a6-4397f66fc5ca.
  ?orgT mandaat:isTijdspecialisatieVan / besluit:classificatie ?clas.
  ?orgT mandaat:bindingEinde ?einde.

  ?orgT org:hasPost ?mandaat.
  GRAPH ?g {
    ?mandataris org:holds ?mandaat.
    OPTIONAL {
      ?mandataris mandaat:einde ?mandaatEinde.
    }
    OPTIONAL {
      ?mandataris dct:modified ?mod.
    }
    FILTER (!BOUND(?mandaatEinde) || ?mandaatEinde > ?einde)
  }
  ?g ext:ownedBy ?someone.
  BIND(NOW() AS ?now)

}