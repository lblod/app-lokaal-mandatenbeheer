  PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
  PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
  PREFIX org: <http://www.w3.org/ns/org#>
  PREFIX lmb: <http://lblod.data.gift/vocabularies/lmb/>
  PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
  PREFIX dcterms: <http://purl.org/dc/terms/>

   INSERT {
    GRAPH ?g {
       ?mandatarisLidGR a mandaat:Mandataris;
            ?pRMW ?oRMW;
            mandaat:rangorde """Gemeenteraadslid""";
            org:holds ?mandaatLidGR;
            dcterms:modified ?now; 
            org:hasMembership ?newMembership.
        ?newMembership a org:Membership;
           mu:uuid ?newMembershipId;
           org:organisation ?fractie.
     }
   }
   WHERE {
    VALUES (?mandatarisLidRMW ?mandatarisLidGR ?mandaatLidGR ?newMembership ?newMembershipId ?kim ?lidRMW ?hedenPeriode) {
      (
        <http://data.lblod.info/id/mandatarissen/ab2edfde-4520-4ec3-b3e1-ce8e022dfa52> # mandataris rmw
        <http://data.lblod.info/id/mandatarissen/46c7189d-b14a-46b6-869e-ba35de8766c0> # new Mandataris
        <http://data.lblod.info/id/mandaten/22241886f8991bc4f961b4dfb2dde822feb38e7787dddd9a3cab615d148705dd> # mandaat lid GR
        <http://www.w3.org/ns/org#lidmaatschappen/2e4f143d-a663-47b4-8fa3-3e78b3117f36> # new membership
        """2e4f143d-a663-47b4-8fa3-3e78b3117f36""" # new membership id
        <http://data.lblod.info/id/personen/cf8d2fffcacfd2f2e02fdd9311930b49aafb5249cd6c484971cdb4d1124e2381> # Kim
        <http://data.vlaanderen.be/id/concept/BestuursfunctieCode/5ab0e9b8a3b2ca7c5e000015> # Lid RMW
        <http://data.lblod.info/id/concept/Bestuursperiode/96efb929-5d83-48fa-bfbb-b98dfb1180c7> # 2024 - heden
      )
    }

     GRAPH ?g {
       ?mandatarisLidRMW a mandaat:Mandataris;
            mandaat:isBestuurlijkeAliasVan ?kim;
            org:holds ?mandaatRMW;
            org:hasMembership / org:organisation ?fractie;
            ?pRMW ?oRMW.
       FILTER (?pRMW NOT IN (mu:uuid, dcterms:modified, org:holds ,mandaat:rangorde, org:hasMembership))
       ?mandaatRMW org:role ?lidRMW.
       ?mandaatRMW ^org:hasPost ?bestuursorgaanRMW.
       ?bestuursorgaanRMW lmb:heeftBestuursperiode ?hedenPeriode.
    }
     BIND(NOW() AS ?now)
   }
