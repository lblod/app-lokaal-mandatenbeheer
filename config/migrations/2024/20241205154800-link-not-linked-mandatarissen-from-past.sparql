PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX lmb: <http://lblod.data.gift/vocabularies/lmb/>
PREFIX bestuursfunctieCode: <http://data.vlaanderen.be/id/concept/BestuursfunctieCode/>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>

INSERT {
  GRAPH <http://mu.semte.ch/graphs/linkedInstances> {
    ?mandataris ext:linked ?linkedMandataris.
  }
}  
WHERE {
  GRAPH ?g {
    ?mandataris a mandaat:Mandataris.
    ?mandataris org:holds ?mandaat.
    ?mandataris mandaat:start ?mandatarisStart.
    ?mandataris mandaat:status ?mandatarisStatus.
    ?mandataris mandaat:isBestuurlijkeAliasVan ?persoon.
    ?mandataris org:hasMembership ?membership.
    ?membership org:organisation ?fractie.
    OPTIONAL {
      ?mandataris mandaat:einde ?mandatarisEinde.
    }
    ?mandaat org:role ?bfCode.
    ?mandaat ^org:hasPost ?bestuursorgaan.
    ?bestuursorgaan mandaat:isTijdspecialisatieVan ?bestuursorgaanInTijd.
    ?bestuursorgaan lmb:heeftBestuursperiode ?bestuursperiode.

    ?linkedMandataris a mandaat:Mandataris.
    ?linkedMandataris org:holds ?lmandaat.
    ?linkedMandataris mandaat:start ?mandatarisStart.
    ?linkedMandataris mandaat:status ?mandatarisStatus.
    ?linkedMandataris mandaat:isBestuurlijkeAliasVan ?persoon.
    ?linkedMandataris org:hasMembership ?lmembership.
    ?lmembership org:organisation ?fractie.
    OPTIONAL {
      ?linkedMandataris mandaat:einde ?mandatarisEinde.
    }
    ?lmandaat org:role ?linkedBfCode.
    ?lmandaat ^org:hasPost ?lbestuursorgaan.
    ?lbestuursorgaan mandaat:isTijdspecialisatieVan ?lbestuursorgaanInTijd.
    ?lbestuursorgaan lmb:heeftBestuursperiode ?bestuursperiode.
    }

    FILTER NOT EXISTS {
    GRAPH <http://mu.semte.ch/graphs/linkedInstances> {
      { ?mandataris ext:linked ?linkedMandataris . } UNION { ?linkedMandataris ext:linked ?mandataris . }
    }
  }
  FILTER (?mandataris != ?linkedMandataris)

  BIND (<http://data.lblod.info/id/concept/Bestuursperiode/96efb929-5d83-48fa-bfbb-b98dfb1180c7> AS ?bestuursperiode) # 2024 - heden
  VALUES (?bfCode ?linkedBfCode) {
    (bestuursfunctieCode:5ab0e9b8a3b2ca7c5e000011 bestuursfunctieCode:5ab0e9b8a3b2ca7c5e000015) # Gemeenteraadslid | Lid RMV
    (bestuursfunctieCode:5ab0e9b8a3b2ca7c5e000012 bestuursfunctieCode:5ab0e9b8a3b2ca7c5e000016) # Voorzitter Gemeenteraad | Voorzitter RMV
    (bestuursfunctieCode:5ab0e9b8a3b2ca7c5e000014 bestuursfunctieCode:5ab0e9b8a3b2ca7c5e000017) # Schepen | Lid Vast Bureau
    (bestuursfunctieCode:5ab0e9b8a3b2ca7c5e000013 bestuursfunctieCode:5ab0e9b8a3b2ca7c5e000018) # Burgemeester | Voorzitter Vast Bureau
    (bestuursfunctieCode:7b038cc40bba10bec833ecfe6f15bc7a bestuursfunctieCode:5ab0e9b8a3b2ca7c5e000018) # Aangewezen burgemeester | Voorzitter Vast Bureau
  }
}
