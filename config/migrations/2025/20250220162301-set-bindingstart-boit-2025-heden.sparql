PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX bestuursperiode: <http://data.lblod.info/id/concept/Bestuursperiode/>
PREFIX lmb: <http://lblod.data.gift/vocabularies/lmb/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX extlmb:  <http://mu.semte.ch/vocabularies/ext/lmb/>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX dct: <http://purl.org/dc/terms/>

DELETE {
  GRAPH ?g {
    ?bestuursorgaanIT mandaat:bindingStart ?bindingStart .
    ?bestuursorgaanIT dct:modified ?modified .
  }
}
INSERT {
  GRAPH ?g {
    ?bestuursorgaanIT mandaat:bindingStart ?earliestStart .
    ?bestuursorgaanIT dct:modified ?now .
  }
}
WHERE {
  {
  SELECT DISTINCT ?g ?bestuursorgaanIT ?bindingStart ?modified (MIN(?start) AS ?earliestStart) WHERE {
    GRAPH ?g {
      ?bestuursorgaan a besluit:Bestuursorgaan ;
        skos:prefLabel ?label ;
        besluit:classificatie ?classificatie .
      ?bestuursorgaanIT a besluit:Bestuursorgaan ;
        mandaat:isTijdspecialisatieVan ?bestuursorgaan ;
        lmb:heeftBestuursperiode bestuursperiode:96efb929-5d83-48fa-bfbb-b98dfb1180c7 ; # 2025 - heden
        org:hasPost / ^org:holds ?mandataris .
      OPTIONAL {
        ?bestuursorgaanIT mandaat:bindingStart ?bindingStart .
      }
      OPTIONAL {
        ?bestuursorgaanIT dct:modified ?modified .
      }
      ?mandataris mandaat:start ?start .
    }
    ?classificatie skos:inScheme extlmb:gemeente-bestuursorgaan-codes .
    ?g ext:ownedBy ?bestuurseenheid .
    ?bestuurseenheid ext:voorbereidingVerborgen true .
  }
  GROUP BY ?g ?bestuursorgaanIT ?bindingStart ?modified
  }
  BIND(NOW() as ?now)
}
