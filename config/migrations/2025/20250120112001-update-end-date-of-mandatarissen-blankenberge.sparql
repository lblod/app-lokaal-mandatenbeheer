PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX lmb: <http://lblod.data.gift/vocabularies/lmb/>

DELETE {
  GRAPH ?g { 
    ?mandataris mandaat:einde "2024-12-01T23:00:00Z"^^xsd:dateTime.
    ?mandataris dcterms:modified ?modified.
  }
}
INSERT {
  GRAPH ?g { 
    ?mandataris mandaat:einde """2024-12-15"""^^xsd:date.
    ?mandataris dcterms:modified ?now.
  }
}
WHERE {
  GRAPH ?g {
    ?mandataris mandaat:einde "2024-12-01T23:00:00Z"^^xsd:dateTime.
    OPTIONAL {
      ?mandataris dcterms:modified ?modified.
    }

    ?mandataris org:holds / ^org:hasPost ?boit .
    ?boit mandaat:isTijdspecialisatieVan ?bestuursorgaan .
    ?boit lmb:heeftBestuursperiode <http://data.lblod.info/id/concept/Bestuursperiode/a2b977a3-ce68-4e42-80a6-4397f66fc5ca>. #2019 - 2024
    ?bestuursorgaan besluit:classificatie ?classificatie .

    FILTER(?classificatie IN(
      <http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/5ab0e9b8a3b2ca7c5e000006>, # CBS
      <http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/5ab0e9b8a3b2ca7c5e000008> # Vast Bureau
    ))
  }
  ?g ext:ownedBy ?blankenberge.
  FILTER(?blankenberge IN(
    <http://data.lblod.info/id/bestuurseenheden/6377f53f54990033c90de6101e263f4d9e41eb7c3e4f70dec48caccefc253760>, # Gemeente Blankenberge
    <http://data.lblod.info/id/bestuurseenheden/56061c5a09c4e4fab34edffa2585bd47b5c104edea687e409a9a926b2197850d> # OCMW Blankenberge
  ))
  BIND(NOW() as ?now)
} 
