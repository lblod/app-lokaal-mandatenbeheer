PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX lmb: <http://lblod.data.gift/vocabularies/lmb/>
DELETE {
  GRAPH ?g {
    ?mandataris org:hasMembership ?lidmaatschap .
    ?lidmaatschap ?lp ?lo .
  }
}
WHERE {
  GRAPH ?g {
    ?mandataris org:holds ?mandaat .
  }
  ?g ext:ownedBy ?eenheid .
  ?boi org:hasPost ?mandaat .
  ?boi lmb:heeftBestuursperiode <http://data.lblod.info/id/concept/Bestuursperiode/96efb929-5d83-48fa-bfbb-b98dfb1180c7> . # 2024- heden
  ?boi mandaat:isTijdspecialisatieVan ?bestuursorgaan .
  ?bestuursorgaan besluit:classificatie <http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/5ab0e9b8a3b2ca7c5e000009>  . # BCSD
  ?mandataris mandaat:isBestuurlijkeAliasVan ?persoon .
  ?mandataris org:hasMembership ?lidmaatschap .
  ?lidmaatschap ?lp ?lo .
  ?lidmaatschap org:organisation ?fractie .
  ?bestuursorgaan besluit:bestuurt / besluit:classificatie <http://data.vlaanderen.be/id/concept/BestuurseenheidClassificatieCode/5ab0e9b8a3b2ca7c5e000002> . # OCMW
}
