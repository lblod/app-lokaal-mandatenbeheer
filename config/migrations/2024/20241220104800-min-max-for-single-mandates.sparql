  PREFIX org: <http://www.w3.org/ns/org#>
  PREFIX lmb: <http://lblod.data.gift/vocabularies/lmb/>
  PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
  DELETE {
    GRAPH ?g {
      ?mandaat lmb:minAantalHouders ?oldMin.
      ?mandaat lmb:maxAantalHouders ?oldMax.
    }
  }
  INSERT {
    GRAPH ?g {
      ?mandaat lmb:minAantalHouders 1 .
      ?mandaat lmb:maxAantalHouders 1 .
    }
  }
  WHERE {
    VALUES ?role {
      <http://data.vlaanderen.be/id/concept/BestuursfunctieCode/5ab0e9b8a3b2ca7c5e000012> #              Voorzitter van de gemeenteraad
      <http://data.vlaanderen.be/id/concept/BestuursfunctieCode/5ab0e9b8a3b2ca7c5e000013> #              Burgemeester
      <http://data.vlaanderen.be/id/concept/BestuursfunctieCode/5ab0e9b8a3b2ca7c5e000016> #              Voorzitter van de Raad voor Maatschappelijk Welzijn
      <http://data.vlaanderen.be/id/concept/BestuursfunctieCode/5ab0e9b8a3b2ca7c5e000018> #              Voorzitter van het Vast bureau
      <http://data.vlaanderen.be/id/concept/BestuursfunctieCode/5ab0e9b8a3b2ca7c5e00001a> #              Voorzitter van het Bijzonder Comit√© voor de Sociale Dienst
      <http://data.vlaanderen.be/id/concept/BestuursfunctieCode/5ab0e9b8a3b2ca7c5e00001c> #              Voorzitter van de districtsraad
      <http://data.vlaanderen.be/id/concept/BestuursfunctieCode/5ab0e9b8a3b2ca7c5e00001d> #              Districtsburgemeester
      <http://data.vlaanderen.be/id/concept/BestuursfunctieCode/5ab0e9b8a3b2ca7c5e000026> #              Voorzitter van het districtscollege
      <http://data.vlaanderen.be/id/concept/BestuursfunctieCode/5ab0e9b8a3b2ca7c5e000027> #              Voorzitter van de provincieraad
      <http://data.vlaanderen.be/id/concept/BestuursfunctieCode/7b038cc40bba10bec833ecfe6f15bc7a> #      Aangewezen burgemeester
      <http://data.vlaanderen.be/id/concept/BestuursfunctieCode/d7c00cd1-baf1-4346-83c0-6796c0bedd85> #  Gouverneur

    }

    GRAPH ?g {
      ?mandaat org:role ?role.
      OPTIONAL {
        ?mandaat lmb:minAantalHouders ?oldMin.
      }
      OPTIONAL {
        ?mandaat lmb:maxAantalHouders ?oldMax.
      }
    }
    ?g ext:ownedBy ?someone.
  }
