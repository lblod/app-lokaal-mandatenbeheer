
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX bestuursperiode: <http://data.lblod.info/id/concept/Bestuursperiode/>
PREFIX lmb: <http://lblod.data.gift/vocabularies/lmb/>

DELETE {
  GRAPH ?g {
    ?mandataris dct:modified ?modified .
    ?mandataris mandaat:einde ?mandatarisEinde .
  }
}
INSERT {
  GRAPH ?g {
    ?mandataris dct:modified ?now .
    ?mandataris mandaat:einde ?boiEinde .
  }
}
WHERE {
  ?boi mandaat:bindingEinde ?boiEinde .
  ?boi lmb:heeftBestuursperiode ?period .
  ?boi mandaat:isTijdspecialisatieVan ?orgaan .
  ?boi org:hasPost ?mandaat .
  GRAPH ?g  {
    ?mandataris org:holds ?mandaat .
    OPTIONAL {
      ?mandataris dct:modified ?modified .
    }
    FILTER NOT EXISTS {
      ?mandataris mandaat:einde ?mandatarisEinde.
    }
  }
  ?g ext:ownedBy ?eenheid .
  FILTER (?period != bestuursperiode:96efb929-5d83-48fa-bfbb-b98dfb1180c7) # 2024 - heden
  BIND(NOW() AS ?now)
}
