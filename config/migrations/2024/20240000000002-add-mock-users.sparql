PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX mu:<http://mu.semte.ch/vocabularies/core/>
PREFIX ext:<http://mu.semte.ch/vocabularies/ext/>
PREFIX bestuurseenheidscode: <http://data.vlaanderen.be/id/concept/BestuurseenheidClassificatieCode/>

INSERT {
  GRAPH ?g {
    ?persoon a foaf:Person;
            mu:uuid ?uuidPersoon;
            foaf:firstName ?classificatie;
            foaf:familyName ?naam;
            foaf:member ?bestuurseenheid;
            foaf:account ?account.
    ?account a foaf:OnlineAccount;
            mu:uuid ?uuidAccount;
            foaf:accountServiceHomepage <https://github.com/lblod/mock-login-service>;
            ext:sessionRole "LoketLB-mandaatGebruiker".
  }
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?persoon a foaf:Person;
            mu:uuid ?uuidPersoon;
            foaf:firstName ?classificatie;
            foaf:familyName ?naam;
            foaf:member ?bestuurseenheid;
            foaf:account ?account.
    ?account a foaf:OnlineAccount;
            mu:uuid ?uuidAccount;
            foaf:accountServiceHomepage <https://github.com/lblod/mock-login-service>;
            ext:sessionRole "LoketLB-mandaatGebruiker".
  }
}
WHERE {
     ?bestuurseenheid a besluit:Bestuurseenheid;
        besluit:classificatie ?bestuurseenheidscodes;
     mu:uuid ?id;
     skos:prefLabel ?naam;
     besluit:classificatie/skos:prefLabel ?classificatie.
     # provincies, gemeentes, ocmws en districten
     FILTER (?bestuurseenheidscodes IN ( bestuurseenheidscode:5ab0e9b8a3b2ca7c5e000000, bestuurseenheidscode:5ab0e9b8a3b2ca7c5e000001, bestuurseenheidscode:5ab0e9b8a3b2ca7c5e000002, bestuurseenheidscode:5ab0e9b8a3b2ca7c5e000003 ))
     BIND(CONCAT(?classificatie, " ", ?naam) as ?volledigeNaam)
     BIND(MD5(?volledigeNaam) as ?uuidPersoon)
     BIND(MD5(CONCAT(?volledigeNaam,"ACCOUNT")) as ?uuidAccount)
     BIND(IRI(CONCAT("http://data.lblod.info/id/persoon/", ?uuidPersoon)) AS ?persoon)
     BIND(IRI(CONCAT("http://data.lblod.info/id/account/", ?uuidAccount)) AS ?account)
     BIND(IRI(CONCAT("http://mu.semte.ch/graphs/organizations/", ?id)) AS ?g)
}