PREFIX org: <http://www.w3.org/ns/org#>
PREFIX lmb: <http://lblod.data.gift/vocabularies/lmb/>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX dct: <http://purl.org/dc/terms/>

DELETE {
  GRAPH ?g {
    ?mandataris lmb:hasPublicationStatus <http://data.lblod.info/id/concept/MandatarisPublicationStatusCode/588ce330-4abb-4448-9776-a17d9305df07> . # Draft publication status
    ?mandataris dct:modified ?modified .
  }
}
INSERT {
  GRAPH ?g {
    ?mandataris lmb:hasPublicationStatus <http://data.lblod.info/id/concept/MandatarisPublicationStatusCode/d3b12468-3720-4cb0-95b4-6aa2996ab188> . # Niet bekrachtigd publication status
    ?mandataris dct:modified ?now .
  }
}
WHERE {
    GRAPH ?g {
      ?mandataris lmb:hasPublicationStatus <http://data.lblod.info/id/concept/MandatarisPublicationStatusCode/588ce330-4abb-4448-9776-a17d9305df07> . # Draft publication status
      ?mandataris org:holds / ^org:hasPost ?boi .

      OPTIONAL {
        ?mandataris dct:modified ?modified .
      } 
      {{
        VALUES ?bestuursperiode {
          <http://data.lblod.info/id/concept/Bestuursperiode/845dbc7f-139e-4632-b200-f90e180f1dba> # 2012 - 2019
          <http://data.lblod.info/id/concept/Bestuursperiode/a2b977a3-ce68-4e42-80a6-4397f66fc5ca> # 2019 - 2024
          <http://data.lblod.info/id/concept/Bestuursperiode/9486222f-2696-4811-bde1-fef9dc4b5f68> # Overige
        }
        ?boi lmb:heeftBestuursperiode ?bestuursperiode .
      } UNION {
        ?boi lmb:heeftBestuursperiode <http://data.lblod.info/id/concept/Bestuursperiode/96efb929-5d83-48fa-bfbb-b98dfb1180c7> . # 2024 - heden
        ?iv lmb:hasStatus <http://data.lblod.info/id/concept/InstallatievergaderingStatus/c9fc3292-1576-4a82-8dcd-60795e22131f> . # Behandeld
        ?eenheid  ext:voorbereidingVerborgen true . 
      }}
    }
  ?g ext:ownedBy ?eenheid .
  BIND(NOW() AS ?now)
}
