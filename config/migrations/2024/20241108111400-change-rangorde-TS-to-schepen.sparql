PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX bestuursfunctieCode: <http://data.vlaanderen.be/id/concept/BestuursfunctieCode/>

DELETE {
  GRAPH ?graph {
  ?mandataris mandaat:rangorde ?rangorde.
  }
}
INSERT {
  GRAPH ?graph {
    ?mandataris mandaat:rangorde ?newLabel.
  }
}
WHERE {
  GRAPH ?graph {
    ?mandataris a mandaat:Mandataris.
    ?mandataris org:holds ?mandaat.
    ?mandataris mandaat:rangorde ?rangorde.

    VALUES ?schepenMandaat {
      bestuursfunctieCode:5ab0e9b8a3b2ca7c5e000014 # Schepen
      bestuursfunctieCode:59a90e03-4f22-4bb9-8c91-132618db4b38 # Toegevoegde schepen
      bestuursfunctieCode:5ab0e9b8a3b2ca7c5e000017 # Lid van het Vast Bureau
      bestuursfunctieCode:5ab0e9b8a3b2ca7c5e000015 # Lid van de Raad voor Maatschappelijk Welzijn
      bestuursfunctieCode:5ab0e9b8a3b2ca7c5e00001a # Voorzitter van het Bijzonder Comit√© voor de Sociale Dienst
      bestuursfunctieCode:5ab0e9b8a3b2ca7c5e00001e # District schepen
    } 
    ?mandaat org:role ?schepenMandaat.

    FILTER regex(str(?rangorde), "(Toegevoegde schepen|Districtsschepen|Schepen)$")
    BIND(REPLACE(str(?rangorde), "(Toegevoegde schepen|Districtsschepen|Schepen)$", "schepen") AS ?newLabel)
  }
}