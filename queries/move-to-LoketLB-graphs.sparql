PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX lblodlg: <http://data.lblod.info/vocabularies/leidinggevenden/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX extlmb: <http://mu.semte.ch/vocabularies/ext/lmb/>
PREFIX schema: <http://schema.org/>
PREFIX person: <http://www.w3.org/ns/person#>
PREFIX temp: <http://mu.semte.ch/graph/temp/>

# Inserting bestuurseenheid
# Should stay in public graph
# INSERT {
#   GRAPH ?targetGraph {
#     ?bestuurseenheid ?p ?o.
#     ?os ?op ?bestuurseenheid.
#   }
# } WHERE {
#   GRAPH <http://mu.semte.ch/graphs/public> {
#     ?bestuurseenheid a besluit:Bestuurseenheid;
#                      mu:uuid ?id.
#     {
#       # Outgoing
#       ?bestuurseenheid ?p ?o.
#       VALUES ?p { rdf:type mu:uuid skos:prefLabel skos:altLabel ext:wilMailOntvangen ext:mailAdresVoorNotificaties ext:isTrailUser ext:viewOnlyModules
#                   besluit:werkingsgebied ext:inProvincie besluit:classificatie }
#     }
#     UNION
#     {
#       # Incoming
#       ?os ?op ?bestuurseenheid.
#       VALUES ?op { ext:contactVoor besluit:bestuurt ext:origineleBestuurseenheid }
#     }
#   }
#   BIND(IRI(CONCAT("http://mu.semte.ch/graphs/organizations/", ?id, "/LoketLB-mandaatGebruiker")) AS ?targetGraph)
# };
# Inserting bestuurseenheid-contact
# bestuurseenheid-contact doesn't exit in the loket data

# Inserting werkingsgebied
# Should stay in public graph
# INSERT {
#   GRAPH ?targetGraph {
#     ?werkingsgebied ?p ?o.
#   }
# } WHERE {
#   GRAPH ?targetGraph {
#     ?bestuurseenheid besluit:werkingsgebied ?werkingsgebied.
#   }
#   GRAPH <http://mu.semte.ch/graphs/public> {
#     ?werkingsgebied a prov:Location;
#                     ?p ?o.
#     VALUES ?p { rdf:type mu:uuid rdfs:label ext:werkingsgebiedNiveau }
#   }
# };

INSERT {
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?bestuurseenheid ext:inGraph ?targetGraph.
  }
} WHERE {
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?bestuurseenheid a besluit:Bestuurseenheid;
                     mu:uuid ?id.
  }
  BIND(IRI(CONCAT("http://mu.semte.ch/graphs/organizations/", ?id, "/LoketLB-mandaatGebruiker")) AS ?targetGraph)
};

# UNDER CONSTRUCTION, IN THE PROCESS OF ADDING ext:inGraph
# Inserting bestuursorgaan
INSERT {
  GRAPH ?targetGraph {
    ?orgaan ?p ?o.
    ?os ?op ?orgaan.
    ?orgaan ext:inGraph ?targetGraph.
  }
} WHERE {
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?bestuurseenheid a besluit:Bestuurseenheid;
                     ext:inGraph ?targetGraph;
                     ^besluit:bestuurt ?orgaan.
    ?orgaan a besluit:Bestuursorgaan.
    {
      ?orgaan ?p ?o.
      VALUES ?p { rdf:type mu:uuid skos:prefLabel ext:deactivatedAt
                  besluit:bestuurt ext:origineleBestuurseenheid besluit:classificatie ext:origineleBestuursorgaan }
    }
    UNION
    {
      ?os ?op ?orgaan.
      VALUES ?op { mandaat:isTijdspecialisatieVan }
    }
  }
};
# Inserting bestuursorgaan in de tijd
INSERT {
  GRAPH ?targetGraph {
    ?orgaanInTijd ?p ?o.
    ?os ?op ?orgaanInTijd.
  }
} WHERE {
  ?orgaan ext:inGraph ?targetGraph;
          ^mandaat:isTijdspecialisatieVan ?orgaanInTijd.
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?orgaanInTijd a besluit:Bestuursorgaan.
    {
      ?orgaanInTijd ?p ?o.
      VALUES ?p { rdf:type mu:uuid dct:modified ext:deactivatedAt mandaat:bindingEinde mandaat:bindingStart
                  mandaat:isTijdspecialisatieVan org:hasPost lblodlg:heeftBestuursfunctie ext:heeftBestuursperiode }      
    }
    UNION
    {
      ?os ?op ?orgaanInTijd.
      VALUES ?op { mandaat:steltSamen }
    }
  }
};
# Inserting mandaat
INSERT {
  GRAPH ?targetGraph {
    ?mandaat ?p ?o.
    ?mandataris org:holds ?mandaat.
  }
} WHERE {
  ?orgaan ext:inGraph ?targetGraph;
          ^mandaat:isTijdspecialisatieVan ?orgaanInTijd.
  ?orgaanInTijd org:hasPost ?mandaat.
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?mandaat a mandaat:Mandaat.
    {
      ?mandaat ?p ?o.
      VALUES ?p { rdf:type mu:uuid mandaat:aantalHouders 
                  org:role }
    }
    UNION
    {
      ?mandataris org:holds ?mandaat.
    }
  }
};
# Inserting mandataris
# The mandatarissen are already in the LoketLB-mandaatGebruiker graphs

# Inserting lidmaatschap
# The lidmaatschappen are already in the LoketLB-mandaatGebruiker graphs

# Inserting persoon
# The personen are already in the LoketLB-mandaatGebruiker graphs

# Inserting rechtstreekse-verkiezing
INSERT {
  GRAPH ?targetGraph {
    ?verkiezing ?p ?o.
    ?kandidatenlijst mandaat:behoortTot ?verkiezing.
  }
} WHERE {
  ?orgaan ext:inGraph ?targetGraph;
          ^mandaat:isTijdspecialisatieVan ?orgaanInTijd.
  ?orgaanInTijd ^mandaat:steltSamen ?verkiezing.
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?verkiezing a mandaat:RechtstreekseVerkiezing.
    {
      ?verkiezing ?p ?o.
      VALUES ?p { rdf:type mu:uuid mandaat:datum dct:valid 
                  mandaat:steltSamen }
    }
    UNION
    {
      ?kandidatenlijst mandaat:behoortTot ?verkiezing.
    }
  }
};
# Inserting kandidatenlijst
INSERT {
  GRAPH ?targetGraph {
    ?kandidatenlijst ?p ?o.
    ?resultaat mandaat:isResultaatVoor ?kandidatenlijst.
  }
} WHERE {
  ?orgaan ext:inGraph ?targetGraph;
          ^mandaat:isTijdspecialisatieVan ?orgaanInTijd.
  ?orgaanInTijd ^mandaat:steltSamen ?verkiezing.
  ?verkiezing ^mandaat:behoortTot ?kandidatenlijst.
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?kandidatenlijst a mandaat:Kandidatenlijst.
    {
      ?kandidatenlijst ?p ?o.
      VALUES ?p { rdf:type mu:uuid skos:prefLabel mandaat:lijstnummer
                  mandaat:lijsttype mandaat:behoortTot mandaat:heeftKandidaat ext:produceertFractie }
    }
    UNION
    {
      ?resultaat mandaat:isResultaatVoor ?kandidatenlijst.
    }
  }
};
# Inserting verkiezingsresultaat
INSERT {
  GRAPH ?targetGraph {
    ?resultaat ?p ?o.
  }
} WHERE {
  ?orgaan ext:inGraph ?targetGraph;
          ^mandaat:isTijdspecialisatieVan ?orgaanInTijd.
  ?orgaanInTijd ^mandaat:steltSamen ?verkiezing.
  ?verkiezing ^mandaat:behoortTot ?kandidatenlijst.
  ?kandidatenlijst ^mandaat:isResultaatVoor ?resultaat.
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?resultaat a mandaat:Verkiezingsresultaat;
               ?p ?o.
    VALUES ?p { rdf:type mu:uuid mandaat:aantalNaamstemmen mandaat:plaatsRangorde
                mandaat:isResultaatVan mandaat:isResultaatVoor mandaat:gevolg }
  }
};

DELETE {
  GRAPH ?g {
    ?s ext:inGraph ?o.
  }
}
WHERE {
  GRAPH ?g {
    ?s ext:inGraph ?o.
  }
};