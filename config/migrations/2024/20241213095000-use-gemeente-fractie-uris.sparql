  PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
  PREFIX org: <http://www.w3.org/ns/org#>
  PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
  PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
  PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
  PREFIX regorg: <https://www.w3.org/ns/regorg#>
  PREFIX lmb: <http://lblod.data.gift/vocabularies/lmb/>
  PREFIX dct: <http://purl.org/dc/terms/>
  PREFIX astreams: <http://www.w3.org/ns/activitystreams#>
  DELETE {
    GRAPH ?ocmwg {
      ?fractieOCMW ?p ?o.
      ?membership dct:modified ?oldModified.
    }
  }
  INSERT {
    GRAPH ?ocmwg {
      ?fractieOCMW a <http://www.w3.org/ns/activitystreams#Tombstone>.
      ?fractieOCMW dct:modified ?now .
      ?fractieOCMW astreams:deleted ?now.
      ?fractieOCMW astreams:formerType mandaat:Fractie .
      ?fractieOCMW dct:isReplacedBy ?fractieG .
      ?membership dct:modified ?now.
      ?membership org:organisation ?fractieG.
      ?fractieG ?p ?o.
      ?fractieG org:memberOf ?ocmwOrgInT.
  }
  }
  WHERE {
    GRAPH ?ocmwg {
      ?mandataris org:hasMembership ?membership.
      ?membership org:organisation ?fractieOCMW.
      ?fractieOCMW org:memberOf ?ocmwOrgInT.
      ?fractieOCMW regorg:legalName ?name.
      ?ocmwOrgInT lmb:heeftBestuursperiode <http://data.lblod.info/id/concept/Bestuursperiode/96efb929-5d83-48fa-bfbb-b98dfb1180c7>.
      ?fractieOCMW ?p ?o.
      OPTIONAL {
        ?membership dct:modified ?oldModified.
      }
    }
    GRAPH ?gemeenteg {
      ?fractieG org:memberOf ?gemeenteOrgInT.
      ?fractieG regorg:legalName ?name.
      ?gemeenteOrgInT lmb:heeftBestuursperiode <http://data.lblod.info/id/concept/Bestuursperiode/96efb929-5d83-48fa-bfbb-b98dfb1180c7>.
    }
    ?ocmwg ext:ownedBy ?ocmweenheid.
    ?ocmweenheid besluit:classificatie <http://data.vlaanderen.be/id/concept/BestuurseenheidClassificatieCode/5ab0e9b8a3b2ca7c5e000002>. # OCMW classificatie
    ?gemeenteg ext:ownedBy ?gemeenteEenheid.
    ?ocmweenheid ext:isOCMWVoor ?gemeenteEenheid.
    FILTER (?fractieOCMW != ?fractieG)
    BIND(NOW() AS ?now)
  }