PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX lmb: <http://lblod.data.gift/vocabularies/lmb/>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX astreams: <http://www.w3.org/ns/activitystreams#>
PREFIX dct: <http://purl.org/dc/terms/>

DELETE {
  GRAPH ?g {  
    ?mandataris ?p ?o.
    ?membership ?memberP ?memberO.
  }
}
INSERT {
GRAPH ?g {
  ?mandataris a astreams:Tombstone.
  ?mandataris dct:modified ?now.
  ?mandataris astreams:deleted ?now.
  ?mandataris astreams:formerType mandaat:Mandataris.

  ?membership a astreams:Tombstone.
  ?membership dct:modified ?now.
  ?membership astreams:deleted ?now.
  ?membership astreams:formerType org:Membership.
}
}
WHERE {
  GRAPH ?g {
    VALUES ?gemeente {
      <http://data.lblod.info/id/bestuurseenheden/72af5d601b193f4fcf18154d643d89481082ecd3eff61720f1cf118057666ac5> # Sint-Genesius-Rode
      <http://data.lblod.info/id/bestuurseenheden/7db7a009ab84e4d98e08ec4673b94605584f65ae5ed7a1d8decb23fb990ff73f> # Linkebeek
      <http://data.lblod.info/id/bestuurseenheden/4c1ddb45485a5dc672a57e8347318cf5d60a16fc2e527ad055d3f69654e11b5a> # Drogenbos
      <http://data.lblod.info/id/bestuurseenheden/4fb8eb24aa9d5561f3cd7b502715fc758f41b71aafa13b872ee8aeb1d027dbe9> # Wemmel
      <http://data.lblod.info/id/bestuurseenheden/3b1e0143755729396654f651bfed689681274839c427f9598e930d816fb7a1c8> # Kraainem
      <http://data.lblod.info/id/bestuurseenheden/3b337ebeb02ede5e152c952e2f24aa1bff3ebbb6afb968a647cb05efe72c0237> # Wezembeek-Oppem
    }

    ?tmpOrgaan ext:isTijdelijkOrgaanIn ?gemeente.
    ?tmpOrgaan besluit:classificatie <http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/5ab0e9b8a3b2ca7c5e000009>. # BCSD

    ?boi mandaat:isTijdspecialisatieVan ?tmpOrgaan.
    ?boi lmb:heeftBestuursperiode <http://data.lblod.info/id/concept/Bestuursperiode/96efb929-5d83-48fa-bfbb-b98dfb1180c7>. # 2024 - heden
    ?boi org:hasPost ?mandaat.

    ?mandataris org:holds ?mandaat.
    ?mandataris org:hasMembership ?membership.
    ?mandataris ?p ?o.
    ?membership ?memberP ?memberO.
  }
  ?g ext:ownedBy ?something .
  BIND(NOW() as ?now)
}
