PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX bestuursperiode: <http://data.lblod.info/id/concept/Bestuursperiode/>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX extlmb:  <http://mu.semte.ch/vocabularies/ext/lmb/>

# Nieuwe bestuursorganen in tijd voor Gemeente, OCMW, District en Provincie

INSERT {
  GRAPH ?g {
    ?vorigBestuursorgaanInTijd mandaat:bindingEinde "2024-12-06"^^<http://www.w3.org/2001/XMLSchema#date> .
    ?bestuursorgaanInTijd a besluit:Bestuursorgaan ;
      mandaat:isTijdspecialisatieVan ?bestuursorgaan ;
      mu:uuid ?uuidInTijd ;
      mandaat:bindingStart "2025-01-01"^^<http://www.w3.org/2001/XMLSchema#date> ;
		  mandaat:bindingEinde "2030-12-06"^^xsd:date ;
      ext:heeftBestuursperiode bestuursperiode:96efb929-5d83-48fa-bfbb-b98dfb1180c7 .
  }
}
WHERE {
  ?bestuurseenheid a besluit:Bestuurseenheid;
      skos:prefLabel ?bestuurseenheidLabel;
      besluit:classificatie ?classificatie;
      mu:uuid ?bestuurseenheidId.
  ?bestuursorgaan besluit:bestuurt ?bestuurseenheid ;
      skos:prefLabel ?bestuursorgaanLabel ;
      besluit:classificatie ?bestuursorgaanClassificatie.
  ?bestuursorgaanClassificatie skos:inScheme ?decretaleCodes.
  ?vorigBestuursorgaanInTijd mandaat:isTijdspecialisatieVan ?bestuursorgaan ;
      mandaat:bindingStart "2019-01-01"^^<http://www.w3.org/2001/XMLSchema#date> .
      
  FILTER (?classificatie IN (<http://data.vlaanderen.be/id/concept/BestuurseenheidClassificatieCode/5ab0e9b8a3b2ca7c5e000000>, <http://data.vlaanderen.be/id/concept/BestuurseenheidClassificatieCode/5ab0e9b8a3b2ca7c5e000001>, <http://data.vlaanderen.be/id/concept/BestuurseenheidClassificatieCode/5ab0e9b8a3b2ca7c5e000002>, <http://data.vlaanderen.be/id/concept/BestuurseenheidClassificatieCode/5ab0e9b8a3b2ca7c5e000003>))
  FILTER(?decretaleCodes IN (extlmb:gemeente-bestuursorgaan-codes, extlmb:decretale-bestuursorgaan-codes, extlmb:vrije-bestuursorgaan-codes)).


  BIND(SHA256(CONCAT("3ff5d4aa-6b1b-488d-8b34-d2cb6fa64e45", ":", STR(?bestuursorgaanLabel))) AS ?uuidInTijd) .
  BIND(IRI(CONCAT("http://data.lblod.info/id/bestuursorganen/", STR(?uuidInTijd))) AS ?bestuursorgaanInTijd) .
  BIND(IRI(CONCAT("http://mu.semte.ch/graphs/organizations/", STR(?bestuurseenheidId), "/LoketLB-mandaatGebruiker")) AS ?g).
}

# Mandaten voor nieuwe bestuursorganen

INSERT {
  GRAPH ?g {
    ?mandaat a mandaat:Mandaat ;
      mu:uuid ?uuid ;
      org:role ?role .

    ?newBestuursorgaanInTijd org:hasPost ?mandaat .
  }
} WHERE {
    ?bestuursorgaan a besluit:Bestuursorgaan ;
      skos:prefLabel ?bestuursorgaanLabel .
    ?vorigBestuursorgaanInTijd mandaat:isTijdspecialisatieVan ?bestuursorgaan ;
      ext:heeftBestuursperiode bestuursperiode:a2b977a3-ce68-4e42-80a6-4397f66fc5ca ; # 2019-2025
      org:hasPost ?oldMandaat .
    ?newBestuursorgaanInTijd mandaat:isTijdspecialisatieVan ?bestuursorgaan ;
      ext:heeftBestuursperiode bestuursperiode:96efb929-5d83-48fa-bfbb-b98dfb1180c7 . # 2025-heden
    
  GRAPH ?g {
    ?oldMandaat org:role ?role.
  }
    ?role skos:prefLabel ?mandaatLabel.

    BIND(SHA256(CONCAT("f2373ae0-661f-45b8-8bd3-42b33e10c1a2", ":2025-01-01:", STR(?mandaatLabel), STR(?bestuursorgaanLabel))) AS ?uuid) .
    BIND(IRI(CONCAT("http://data.lblod.info/id/mandaten/", STR(?uuid))) AS ?mandaat) .
}