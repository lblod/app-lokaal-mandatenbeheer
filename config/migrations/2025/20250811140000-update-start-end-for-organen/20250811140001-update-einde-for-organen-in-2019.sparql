PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX bestuursperiode: <http://data.lblod.info/id/concept/Bestuursperiode/>
PREFIX lmb: <http://lblod.data.gift/vocabularies/lmb/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX extlmb:  <http://mu.semte.ch/vocabularies/ext/lmb/>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX dct: <http://purl.org/dc/terms/>

DELETE {
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?boiP mandaat:bindingEinde ?bindingEinde .
    ?boiP dct:modified ?modified .
  }
}
INSERT {
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?boiP mandaat:bindingEinde ?startCurrentPeriod .
    ?boiP dct:modified ?now .
  }
}
WHERE {
  ?bestuursorgaan besluit:classificatie / skos:inScheme extlmb:gemeente-bestuursorgaan-codes .

  ?boi mandaat:isTijdspecialisatieVan ?bestuursorgaan .
  ?boi lmb:heeftBestuursperiode bestuursperiode:96efb929-5d83-48fa-bfbb-b98dfb1180c7 . # 2024 - heden

  GRAPH <http://mu.semte.ch/graphs/public> {
    ?boi mandaat:bindingStart ?startCurrentPeriod .
  } 
  ?boiP mandaat:isTijdspecialisatieVan ?bestuursorgaan .
  ?boiP lmb:heeftBestuursperiode bestuursperiode:a2b977a3-ce68-4e42-80a6-4397f66fc5ca . # 2019 - 2024

  GRAPH <http://mu.semte.ch/graphs/public> {
    OPTIONAL {
      ?boiP mandaat:bindingEinde ?bindingEinde .
    }
  }
  OPTIONAL {
    ?boiP dct:modified ?modified .
  }

  BIND(NOW() AS ?now)
}
