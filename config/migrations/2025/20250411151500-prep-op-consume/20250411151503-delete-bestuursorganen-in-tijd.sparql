  PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
  PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
  PREFIX lmb: <http://lblod.data.gift/vocabularies/lmb/>
  PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
  DELETE {
    GRAPH ?g {
      ?s ?p ?o.
    }
  }
  WHERE {
    ?s a besluit:Bestuursorgaan.
    GRAPH ?g {
      ?s ?p ?o.
    }
    ?eenheid a besluit:Bestuurseenheid .
    ?bestuursorgaan a besluit:Bestuursorgaan .
    ?s mandaat:isTijdspecialisatieVan ?bestuursorgaan . 
    ?bestuursorgaan besluit:bestuurt ?eenheid.
    ?bestuursorgaan besluit:classificatie ?clas.

    FILTER(?clas IN (
<http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/180a2fba-6ca9-4766-9b94-82006bb9c709>, # Gouverneur
<http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/1afce932-53c1-46d8-8aab-90dcc331e67d>, # Politieraad
<http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/4955bd72cd0e4eb895fdbfab08da0284>, # Burgemeester
<http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/5ab0e9b8a3b2ca7c5e000005>, # Gemeenteraad
<http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/5ab0e9b8a3b2ca7c5e000006>, # College van Burgemeester en Schepenen
<http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/5ab0e9b8a3b2ca7c5e000007>, # Raad voor Maatschappelijk Welzijn
<http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/5ab0e9b8a3b2ca7c5e000008>, # Vast Bureau
<http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/5ab0e9b8a3b2ca7c5e000009>, # Bijzonder Comité voor de Sociale Dienst
<http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/5ab0e9b8a3b2ca7c5e00000a>, # Districtsraad
<http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/5ab0e9b8a3b2ca7c5e00000b>, # Districtscollege
<http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/5ab0e9b8a3b2ca7c5e00000c>, # Provincieraad
<http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/5ab0e9b8a3b2ca7c5e00000d>, # Deputatie
<http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/9314533e-891f-4d84-a492-0338af104065>, # Districtsburgemeester
<http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/53c0d8cd-f3a2-411d-bece-4bd83ae2bbc9> # Voorzitter van het Bijzonder Comité voor de Sociale Dienst
                          ))
  FILTER( ?p NOT IN (
                      lmb:deactivatedAt,
                      ext:origineleBestuurseenheid,
                      ext:isTijdelijkOrgaanIn,
                      ext:origineleBestuursorgaan,
                      lmb:heeftBestuursperiode
                      ))
}