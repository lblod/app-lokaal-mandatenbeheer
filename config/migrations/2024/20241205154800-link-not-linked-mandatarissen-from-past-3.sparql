PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX lmb: <http://lblod.data.gift/vocabularies/lmb/>
PREFIX bestuursfunctieCode: <http://data.vlaanderen.be/id/concept/BestuursfunctieCode/>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>

INSERT {
  GRAPH <http://mu.semte.ch/graphs/linkedInstances> {
    ?mandataris ext:linked ?linkedMandataris.
  }
}  
WHERE {
  GRAPH ?g {
    ?mandataris org:holds ?mandaat.
    ?mandataris mandaat:start ?mandatarisStart.
    ?mandataris mandaat:status ?mandatarisStatus.
    ?mandataris mandaat:isBestuurlijkeAliasVan ?persoon.
    ?mandataris org:hasMembership / org:organisation ?fractie.
    OPTIONAL {
      ?mandataris mandaat:einde ?mandatarisEinde.
    }
    ?mandaat org:role ?bfCode.
    ?mandaat ^org:hasPost ?bestuursorgaan.
    ?bestuursorgaan mandaat:isTijdspecialisatieVan ?bestuursorgaanInTijd.
    ?bestuursorgaan lmb:heeftBestuursperiode ?bestuursperiode.

    ?linkedMandataris org:holds ?lmandaat.
    ?linkedMandataris mandaat:start ?mandatarisStart.
    ?linkedMandataris mandaat:status ?mandatarisStatus.
    ?linkedMandataris mandaat:isBestuurlijkeAliasVan ?persoon.
    ?linkedMandataris org:hasMembership / org:organisation ?fractie.
    OPTIONAL {
      ?linkedMandataris mandaat:einde ?mandatarisEinde.
    }
    ?lmandaat org:role ?linkedBfCode.
    ?lmandaat ^org:hasPost ?lbestuursorgaan.
    ?lbestuursorgaan mandaat:isTijdspecialisatieVan ?lbestuursorgaanInTijd.
    ?lbestuursorgaan lmb:heeftBestuursperiode ?bestuursperiode.
  }

  FILTER (?mandataris != ?linkedMandataris)
  BIND (<http://data.lblod.info/id/concept/Bestuursperiode/96efb929-5d83-48fa-bfbb-b98dfb1180c7> AS ?bestuursperiode) # 2024 - heden
  VALUES (?bfCode ?linkedBfCode) {
    (bestuursfunctieCode:5ab0e9b8a3b2ca7c5e000014 bestuursfunctieCode:5ab0e9b8a3b2ca7c5e000017) # Schepen | Lid Vast Bureau
  }
}
