PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX bestuursfunctieCode: <http://data.vlaanderen.be/id/concept/BestuursfunctieCode/>

DELETE {
  GRAPH ?graph {
  ?mandataris mandaat:rangorde ?rangorde.
  }
}
INSERT {
  GRAPH ?graph {
    ?mandataris mandaat:rangorde ?newLabel.
  }
}
WHERE {
  GRAPH ?graph {
    ?mandataris a mandaat:Mandataris.
    ?mandataris org:holds ?mandaat.
    ?mandataris mandaat:rangorde ?rangorde.


    VALUES ?schepenMandaat {
      bestuursfunctieCode:5ab0e9b8a3b2ca7c5e000014 # MANDAAT_SCHEPEN_CODE
      bestuursfunctieCode:59a90e03-4f22-4bb9-8c91-132618db4b38 # MANDAAT_TOEGEVOEGDE_SCHEPEN_CODE
      bestuursfunctieCode:5ab0e9b8a3b2ca7c5e00001e # MANDAAT_DISTRICT_SCHEPEN_CODE
    } 
    ?mandaat org:role ?schepenMandaat.

    FILTER regex(str(?rangorde), "(Toegevoegde schepen|Districtsschepen|Schepen)$")
    BIND(REPLACE(str(?rangorde), "(Toegevoegde schepen|Districtsschepen|Schepen)$", "schepen") AS ?newLabel)
  }
}