PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX bestuursfunctieCode: <http://data.vlaanderen.be/id/concept/BestuursfunctieCode/>

DELETE {
  GRAPH ?graph {
  ?mandataris mandaat:rangorde ?rangorde.
  }
}
INSERT {
  GRAPH ?graph {
    ?mandataris mandaat:rangorde ?newLabel.
  }
}
WHERE {
  GRAPH ?graph {
    ?mandataris a mandaat:Mandataris.
    ?mandataris org:holds ?mandaat.
    ?mandataris mandaat:rangorde ?rangorde.

    VALUES ?schepenen {
      bestuursfunctieCode:5ab0e9b8a3b2ca7c5e000014 # Schepen
      bestuursfunctieCode:59a90e03-4f22-4bb9-8c91-132618db4b38 # Toegevoegde schepen
      bestuursfunctieCode:5ab0e9b8a3b2ca7c5e00001e # District schepen
    } 
    ?mandaat org:role ?schepenen.

    FILTER(
      CONTAINS(LCASE(?rangorde), "gemeenteraadslid") ||
      CONTAINS(LCASE(?rangorde), "lid") ||
      CONTAINS(LCASE(?rangorde), "toegevoegde schepen") ||
      CONTAINS(LCASE(?rangorde), "districtsschepen")
    )
    BIND(REPLACE(str(?rangorde), "(?i)(toegevoegde schepen|districtsschepen|schepen|lid|gemeenteraadslid)", "schepen") AS ?newLabel)
  }
}