PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

DELETE {
  GRAPH ?gRechtsgrond {
    ?rechtsgrond ?gp ?go .
  }
}
WHERE {
  GRAPH ?g {
    ?mandataris a mandaat:Mandataris .
    ?mandataris mandaat:start """2024-12-01T23:00:00Z"""^^xsd:dateTime .
    ?mandataris org:holds ?mandaat .
  }
  ?boi org:hasPost ?mandaat .
  ?boi mandaat:isTijdspecialisatieVan ?bestuursorgaan .
  ?bestuursorgaan besluit:classificatie <http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/5ab0e9b8a3b2ca7c5e000005> . # Gemeenteraad
  ?g ext:ownedBy <http://data.lblod.info/id/bestuurseenheden/393a7495daffabe3dabd5d341673287cedc58485c3c8bc273cbd058e735dfee2> . # Gemeente Gavere

  GRAPH ?gRechtsgrond {
    ?rechtsgrond mandaat:bekrachtigtAanstellingVan|mandaat:bekrachtigtOntslagVan ?mandataris .
    ?rechtsgrond ?gp ?go .
    FILTER(
      ?rechtsgrond NOT IN(<https://gavere-echo.cipalschaubroeck.be/id/besluiten/e34164ec-4ed9-47ca-abf4-268dbdef9ff9>, <https://gavere-echo.cipalschaubroeck.be/id/besluiten/a28226b1-45cd-4a98-9bc2-1e1b980867f6>)
    )
  }
}
