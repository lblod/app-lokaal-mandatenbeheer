PREFIX person: <http://www.w3.org/ns/person#>
PREFIX persoon: <http://data.vlaanderen.be/ns/persoon#>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX adms: <http://www.w3.org/ns/adms#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX astreams: <http://www.w3.org/ns/activitystreams#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX extlmb:  <http://mu.semte.ch/vocabularies/ext/lmb/>

DELETE {
  GRAPH ?g {
    ?altDirk ?pp ?po .
    ?rrn ?rp ?ro .
    ?geboorte ?gp ?go .

    ?mandataris mandaat:isBestuurlijkeAliasVan ?altDirk .
    ?mandataris dct:modified ?oldModified .
  }
}
INSERT {
  GRAPH ?g {
    ?altDirk a astreams:Tombstone .
    ?altDirk dct:modified ?now .
    ?altDirk astreams:deleted ?now .
    ?altDirk astreams:formerType person:Person .
    ?altDirk owl:sameAs ?ogDirk .

    ?mandataris mandaat:isBestuurlijkeAliasVan ?ogDirk .
    ?mandataris dct:modified ?now .

    ?ogDirk extlmb:currentFracties ?cFracties .
  }
}
WHERE {
  GRAPH ?g {
    ?ogDirk a person:Person .
    ?altDirk a person:Person ;
      ?pp ?po .
    OPTIONAL {
      ?mandataris a mandaat:Mandataris ;
        mandaat:isBestuurlijkeAliasVan ?altDirk ;
        dct:modified ?oldModified .
    }
    OPTIONAL {
      ?altDirk extlmb:currentFracties ?cFracties .
    }
    OPTIONAL {
      ?altDirk persoon:heeftGeboorte ?geboorte .
      ?geboorte ?gp ?go .
    }
    OPTIONAL {
      ?altDirk adms:identifier ?rrn .
      ?rrn ?rp ?ro .
    }
  }
  BIND(NOW() AS ?now)
  ?g ext:ownedBy ?bestuurseenheid .
  VALUES ?g {
    <http://mu.semte.ch/graphs/organizations/2b1c96da5ba86a6dbb84af9f53b73d8e3f9e4b670c3ab155c9fcadb735d7c31d/LoketLB-mandaatGebruiker>
    <http://mu.semte.ch/graphs/organizations/800ce18716ba451af47c2e05c2a7bdd29ab9305eaa61068629c1ea2ae6c08f4c/LoketLB-mandaatGebruiker>
  }
  VALUES ?ogDirk {
    <http://data.lblod.info/id/personen/bfcd0b64-7939-43b4-9d8e-693a9d146363>
  }
  VALUES ?altDirk {
    <http://data.lblod.info/id/personen/0d5470a0-7f31-4d4c-83ec-2122ee4a4786>
  }
}