PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX lmb: <http://lblod.data.gift/vocabularies/lmb/>

# Process nb members gemeenteraad
DELETE {
  GRAPH ?graph {
    ?mandaatLid lmb:minAantalHouders ?huidigMinLid ;
        lmb:maxAantalHouders ?huidigMaxLid .
    ?mandaatVoorzitter lmb:minAantalHouders ?huidigMinVoorzitter ;
        lmb:maxAantalHouders ?huidigMaxVoorzitter .
  }
}
INSERT {
  GRAPH ?graph {
    ?mandaatLid lmb:minAantalHouders ?min ;
        lmb:maxAantalHouders ?max .
    ?mandaatVoorzitter lmb:minAantalHouders 1 ;
        lmb:maxAantalHouders 1 .
  }
  GRAPH <http://mu.semte.ch/graph/tmp/load> {
    ?row ext:succes true .
  }
}
WHERE {
  GRAPH <http://mu.semte.ch/graph/tmp/load> {
    ?row a ext:NbMembersObject ;
         ext:bestuurseenheid ?naam1 ;
         ext:leden_max ?max ;
         ext:leden_min ?min ;
         ext:mandaat "gemeenteraad" .
    FILTER (isNumeric(?max) && isNumeric(?min))
  }
  GRAPH ?g {
    ?bestuurseenheid a besluit:Bestuurseenheid ;
      skos:prefLabel ?naam2 ;
      besluit:classificatie <http://data.vlaanderen.be/id/concept/BestuurseenheidClassificatieCode/5ab0e9b8a3b2ca7c5e000001> .
    ?bestuursperiode a lmb:Bestuursperiode ;
      skos:prefLabel "2025 - heden" .
  }
  GRAPH ?h {
    ?bestuursorgaan a besluit:Bestuursorgaan ;
      besluit:bestuurt ?bestuurseenheid ;
      ^mandaat:isTijdspecialisatieVan ?bestuursorgaanIT .
    ?bestuursorgaanIT lmb:heeftBestuursperiode ?bestuursperiode ;
      org:hasPost ?mandaatLid ;
      org:hasPost ?mandaatVoorzitter .
    ?mandaatLid org:role <http://data.vlaanderen.be/id/concept/BestuursfunctieCode/5ab0e9b8a3b2ca7c5e000011> .
    OPTIONAL {
      ?mandaatLid lmb:minAantalHouders ?huidigMinLid ;
        lmb:maxAantalHouders ?huidigMaxLid .
    }
    ?mandaatVoorzitter org:role <http://data.vlaanderen.be/id/concept/BestuursfunctieCode/5ab0e9b8a3b2ca7c5e000012> .
    OPTIONAL {
      ?mandaatVoorzitter lmb:minAantalHouders ?huidigMinVoorzitter ;
        lmb:maxAantalHouders ?huidigMaxVoorzitter .
    }
  }
  BIND (?h AS ?graph)
  FILTER (LCASE(STR(REPLACE(?naam1, "\\s+$", ""))) = LCASE(STR(REPLACE(?naam2, "\\s+$", ""))))
}

# Process nb members cbs
DELETE {
  GRAPH ?graph {
    ?mandaatSchepen lmb:minAantalHouders ?huidigMinSchepen ;
        lmb:maxAantalHouders ?huidigMaxSchepen .
    ?mandaatBurgemeester lmb:minAantalHouders ?huidigMinBurgemeester ;
        lmb:maxAantalHouders ?huidigMaxBurgemeester .
    ?mandaatAangewezenBurgemeester lmb:minAantalHouders ?huidigMinAangewezenBurgemeester ;
        lmb:maxAantalHouders ?huidigMaxAangewezenBurgemeester .
  }
}
INSERT {
  GRAPH ?graph {
    ?mandaatSchepen lmb:minAantalHouders 2 ;
        lmb:maxAantalHouders ?max .
    ?mandaatBurgemeester lmb:minAantalHouders 1 ;
        lmb:maxAantalHouders 1 .
    ?mandaatAangewezenBurgemeester lmb:minAantalHouders 1 ;
        lmb:maxAantalHouders 1 .
  }
  GRAPH <http://mu.semte.ch/graph/tmp/load> {
    ?row ext:succes true .
  }
}
WHERE {
  GRAPH <http://mu.semte.ch/graph/tmp/load> {
    ?row a ext:NbMembersObject ;
         ext:bestuurseenheid ?naam1 ;
         ext:leden_max ?max ;
         ext:leden_min ?min ;
         ext:mandaat "schepenen" .
    FILTER (isNumeric(?max) && isNumeric(?min))
  }
  GRAPH ?g {
    ?bestuurseenheid a besluit:Bestuurseenheid ;
      skos:prefLabel ?naam2 ;
      besluit:classificatie <http://data.vlaanderen.be/id/concept/BestuurseenheidClassificatieCode/5ab0e9b8a3b2ca7c5e000001> .
    ?bestuursperiode a lmb:Bestuursperiode ;
      skos:prefLabel "2025 - heden" .
  }
  GRAPH ?h {
    ?bestuursorgaan a besluit:Bestuursorgaan ;
      besluit:bestuurt ?bestuurseenheid ;
      ^mandaat:isTijdspecialisatieVan ?bestuursorgaanIT .
    ?bestuursorgaanIT lmb:heeftBestuursperiode ?bestuursperiode ;
      org:hasPost ?mandaatSchepen ;
      org:hasPost ?mandaatBurgemeester ;
      org:hasPost ?mandaatAangewezenBurgemeester .
    ?mandaatSchepen org:role <http://data.vlaanderen.be/id/concept/BestuursfunctieCode/5ab0e9b8a3b2ca7c5e000014> .
    OPTIONAL {
      ?mandaatSchepen lmb:minAantalHouders ?huidigMinSchepen ;
        lmb:maxAantalHouders ?huidigMaxSchepen .
    }
    ?mandaatBurgemeester org:role <http://data.vlaanderen.be/id/concept/BestuursfunctieCode/5ab0e9b8a3b2ca7c5e000013> .
    OPTIONAL {
      ?mandaatBurgemeester lmb:minAantalHouders ?huidigMinBurgemeester ;
        lmb:maxAantalHouders ?huidigMaxBurgemeester .
    }
    ?mandaatAangewezenBurgemeester org:role <http://data.vlaanderen.be/id/concept/BestuursfunctieCode/7b038cc40bba10bec833ecfe6f15bc7a> .
    OPTIONAL {
      ?mandaatAangewezenBurgemeester lmb:minAantalHouders ?huidigMinAangewezenBurgemeester ;
        lmb:maxAantalHouders ?huidigMaxAangewezenBurgemeester .
    }
  }
  BIND (?h AS ?graph)
  FILTER (LCASE(STR(REPLACE(?naam1, "\\s+$", ""))) = LCASE(STR(REPLACE(?naam2, "\\s+$", ""))))
}

# Process nb members Vast Bureau -> Mirrors CBS
DELETE {
  GRAPH ?ocmw {
    ?mandaatLid lmb:minAantalHouders ?huidigMinLid ;
        lmb:maxAantalHouders ?huidigMaxLid .
    ?mandaatVoorzitter lmb:minAantalHouders ?huidigMinVoorzitter ;
        lmb:maxAantalHouders ?huidigMaxVoorzitter .
  }
}
INSERT {
  GRAPH ?ocmw {
    ?mandaatLid lmb:minAantalHouders ?huidigMinSchepen ;
        lmb:maxAantalHouders ?huidigMaxSchepen .
    ?mandaatVoorzitter lmb:minAantalHouders 1 ;
        lmb:maxAantalHouders 1 .
  }
}
WHERE {
  GRAPH ?g {
    ?bestuurseenheidGemeente a besluit:Bestuurseenheid ;
      besluit:werkingsgebied ?werkingsgebied ;
      besluit:classificatie <http://data.vlaanderen.be/id/concept/BestuurseenheidClassificatieCode/5ab0e9b8a3b2ca7c5e000001> .
    ?bestuurseenheidOCMW a besluit:Bestuurseenheid ;
      besluit:werkingsgebied ?werkingsgebied ;
      besluit:classificatie <http://data.vlaanderen.be/id/concept/BestuurseenheidClassificatieCode/5ab0e9b8a3b2ca7c5e000002> .
    ?bestuursperiode a lmb:Bestuursperiode ;
      skos:prefLabel "2025 - heden" .
  }
  GRAPH ?gemeente {
    ?bestuursorgaanCBS a besluit:Bestuursorgaan ;
      besluit:bestuurt ?bestuurseenheidGemeente ;
      ^mandaat:isTijdspecialisatieVan ?bestuursorgaanITCBS .
    ?bestuursorgaanITCBS lmb:heeftBestuursperiode ?bestuursperiode ;
      org:hasPost ?mandaatSchepen .
    ?mandaatSchepen org:role <http://data.vlaanderen.be/id/concept/BestuursfunctieCode/5ab0e9b8a3b2ca7c5e000014> .
    OPTIONAL {
      ?mandaatSchepen lmb:minAantalHouders ?huidigMinSchepen ;
        lmb:maxAantalHouders ?huidigMaxSchepen .
    }
  }
  GRAPH ?ocmw {
    ?bestuursorgaanVB a besluit:Bestuursorgaan ;
      besluit:bestuurt ?bestuurseenheidOCMW ;
      ^mandaat:isTijdspecialisatieVan ?bestuursorgaanITVB .
    ?bestuursorgaanITVB lmb:heeftBestuursperiode ?bestuursperiode ;
      org:hasPost ?mandaatLid ;
      org:hasPost ?mandaatVoorzitter .
    ?mandaatLid org:role <http://data.vlaanderen.be/id/concept/BestuursfunctieCode/5ab0e9b8a3b2ca7c5e000017> .
    OPTIONAL {
      ?mandaatLid lmb:minAantalHouders ?huidigMinLid ;
        lmb:maxAantalHouders ?huidigMaxLid .
    }
    ?mandaatVoorzitter org:role <http://data.vlaanderen.be/id/concept/BestuursfunctieCode/5ab0e9b8a3b2ca7c5e000018> .
    OPTIONAL {
      ?mandaatVoorzitter lmb:minAantalHouders ?huidigMinVoorzitter ;
        lmb:maxAantalHouders ?huidigMaxVoorzitter .
    }
  }
  BIND (?ocmw AS ?graph)
}

# Process nb members RMW
DELETE {
  GRAPH ?graph {
    ?mandaatLid lmb:minAantalHouders ?huidigMinLid ;
        lmb:maxAantalHouders ?huidigMaxLid .
    ?mandaatVoorzitter lmb:minAantalHouders ?huidigMinVoorzitter ;
        lmb:maxAantalHouders ?huidigMaxVoorzitter .
  }
}
INSERT {
  GRAPH ?graph {
    ?mandaatLid lmb:minAantalHouders ?min ;
        lmb:maxAantalHouders ?max .
    ?mandaatVoorzitter lmb:minAantalHouders 1 ;
        lmb:maxAantalHouders 1 .
  }
  GRAPH <http://mu.semte.ch/graph/tmp/load> {
    ?row ext:succes true .
  }
}
WHERE {
  GRAPH <http://mu.semte.ch/graph/tmp/load> {
    ?row a ext:NbMembersObject ;
         ext:bestuurseenheid ?naam1 ;
         ext:leden_max ?max ;
         ext:leden_min ?min ;
         ext:mandaat "rmw" .
    FILTER (isNumeric(?max) && isNumeric(?min))
  }
  GRAPH ?g {
    ?bestuurseenheid a besluit:Bestuurseenheid ;
      skos:prefLabel ?naam2 ;
      besluit:classificatie <http://data.vlaanderen.be/id/concept/BestuurseenheidClassificatieCode/5ab0e9b8a3b2ca7c5e000002> .
    ?bestuursperiode a lmb:Bestuursperiode ;
      skos:prefLabel "2025 - heden" .
  }
  GRAPH ?h {
    ?bestuursorgaan a besluit:Bestuursorgaan ;
      besluit:bestuurt ?bestuurseenheid ;
      ^mandaat:isTijdspecialisatieVan ?bestuursorgaanIT .
    ?bestuursorgaanIT lmb:heeftBestuursperiode ?bestuursperiode ;
      org:hasPost ?mandaatLid ;
      org:hasPost ?mandaatVoorzitter .
    ?mandaatLid org:role <http://data.vlaanderen.be/id/concept/BestuursfunctieCode/5ab0e9b8a3b2ca7c5e000015> .
    OPTIONAL {
      ?mandaatLid lmb:minAantalHouders ?huidigMinLid ;
        lmb:maxAantalHouders ?huidigMaxLid .
    }
    ?mandaatVoorzitter org:role <http://data.vlaanderen.be/id/concept/BestuursfunctieCode/5ab0e9b8a3b2ca7c5e000016> .
    OPTIONAL {
      ?mandaatVoorzitter lmb:minAantalHouders ?huidigMinVoorzitter ;
        lmb:maxAantalHouders ?huidigMaxVoorzitter .
    }
  }
  BIND (?h AS ?graph)
  FILTER (LCASE(STR(REPLACE(?naam1, "\\s+$", ""))) = LCASE(STR(REPLACE(?naam2, "\\s+$", ""))))
}

# Process nb members BCSD
DELETE {
  GRAPH ?graph {
    ?mandaatLid lmb:minAantalHouders ?huidigMinLid ;
        lmb:maxAantalHouders ?huidigMaxLid .
    ?mandaatVoorzitter lmb:minAantalHouders ?huidigMinVoorzitter ;
        lmb:maxAantalHouders ?huidigMaxVoorzitter .
  }
}
INSERT {
  GRAPH ?graph {
    ?mandaatLid lmb:minAantalHouders ?min ;
        lmb:maxAantalHouders ?max .
    ?mandaatVoorzitter lmb:minAantalHouders 1 ;
        lmb:maxAantalHouders 1 .
  }
  GRAPH <http://mu.semte.ch/graph/tmp/load> {
    ?row ext:succes true .
  }
}
WHERE {
  GRAPH <http://mu.semte.ch/graph/tmp/load> {
    ?row a ext:NbMembersObject ;
         ext:bestuurseenheid ?naam1 ;
         ext:leden_max ?max ;
         ext:leden_min ?min ;
         ext:mandaat "bcsd" .
    FILTER (isNumeric(?max) && isNumeric(?min))
  }
  GRAPH ?g {
    ?bestuurseenheid a besluit:Bestuurseenheid ;
      skos:prefLabel ?naam2 ;
      besluit:classificatie <http://data.vlaanderen.be/id/concept/BestuurseenheidClassificatieCode/5ab0e9b8a3b2ca7c5e000002> .
    ?bestuursperiode a lmb:Bestuursperiode ;
      skos:prefLabel "2025 - heden" .
  }
  GRAPH ?h {
    ?bestuursorgaan a besluit:Bestuursorgaan ;
      besluit:bestuurt ?bestuurseenheid ;
      ^mandaat:isTijdspecialisatieVan ?bestuursorgaanIT .
    ?bestuursorgaanIT lmb:heeftBestuursperiode ?bestuursperiode ;
      org:hasPost ?mandaatLid ;
      org:hasPost ?mandaatVoorzitter .
    ?mandaatLid org:role <http://data.vlaanderen.be/id/concept/BestuursfunctieCode/5ab0e9b8a3b2ca7c5e000019> .
    OPTIONAL {
      ?mandaatLid lmb:minAantalHouders ?huidigMinLid ;
        lmb:maxAantalHouders ?huidigMaxLid .
    }
    ?mandaatVoorzitter org:role <http://data.vlaanderen.be/id/concept/BestuursfunctieCode/5ab0e9b8a3b2ca7c5e00001a> .
    OPTIONAL {
      ?mandaatVoorzitter lmb:minAantalHouders ?huidigMinVoorzitter ;
        lmb:maxAantalHouders ?huidigMaxVoorzitter .
    }
  }
  BIND (?h AS ?graph)
  FILTER (LCASE(STR(REPLACE(?naam1, "\\s+$", ""))) = LCASE(STR(REPLACE(?naam2, "\\s+$", ""))))
}

# Process nb members Districtsraad
DELETE {
  GRAPH ?graph {
    ?mandaatLid lmb:minAantalHouders ?huidigMinLid ;
        lmb:maxAantalHouders ?huidigMaxLid .
    ?mandaatVoorzitter lmb:minAantalHouders ?huidigMinVoorzitter ;
        lmb:maxAantalHouders ?huidigMaxVoorzitter .
  }
}
INSERT {
  GRAPH ?graph {
    ?mandaatLid lmb:minAantalHouders ?min ;
        lmb:maxAantalHouders ?max .
    ?mandaatVoorzitter lmb:minAantalHouders 1 ;
        lmb:maxAantalHouders 1 .
  }
  GRAPH <http://mu.semte.ch/graph/tmp/load> {
    ?row ext:succes true .
  }
}
WHERE {
  GRAPH <http://mu.semte.ch/graph/tmp/load> {
    ?row a ext:NbMembersObject ;
         ext:bestuurseenheid ?naam1 ;
         ext:leden_max ?max ;
         ext:leden_min ?min ;
         ext:mandaat "districtraad" .
    FILTER (isNumeric(?max) && isNumeric(?min))
  }
  GRAPH ?g {
    ?bestuurseenheid a besluit:Bestuurseenheid ;
      skos:prefLabel ?naam2 ;
      besluit:classificatie <http://data.vlaanderen.be/id/concept/BestuurseenheidClassificatieCode/5ab0e9b8a3b2ca7c5e000003> .
    ?bestuursperiode a lmb:Bestuursperiode ;
      skos:prefLabel "2025 - heden" .
  }
  GRAPH ?h {
    ?bestuursorgaan a besluit:Bestuursorgaan ;
      besluit:bestuurt ?bestuurseenheid ;
      ^mandaat:isTijdspecialisatieVan ?bestuursorgaanIT .
    ?bestuursorgaanIT lmb:heeftBestuursperiode ?bestuursperiode ;
      org:hasPost ?mandaatLid ;
      org:hasPost ?mandaatVoorzitter .
    ?mandaatLid org:role <http://data.vlaanderen.be/id/concept/BestuursfunctieCode/5ab0e9b8a3b2ca7c5e00001b> .
    OPTIONAL {
      ?mandaatLid lmb:minAantalHouders ?huidigMinLid ;
        lmb:maxAantalHouders ?huidigMaxLid .
    }
    ?mandaatVoorzitter org:role <http://data.vlaanderen.be/id/concept/BestuursfunctieCode/5ab0e9b8a3b2ca7c5e00001c> .
    OPTIONAL {
      ?mandaatVoorzitter lmb:minAantalHouders ?huidigMinVoorzitter ;
        lmb:maxAantalHouders ?huidigMaxVoorzitter .
    }
  }
  BIND (?h AS ?graph)
  FILTER (LCASE(STR(REPLACE(?naam1, "\\s+$", ""))) = LCASE(STR(REPLACE(?naam2, "\\s+$", ""))))
}